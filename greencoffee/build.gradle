apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

def releaseVersion = '2.7.0'
def bintrayUser = hasProperty('bintray_user') ? bintray_user : ''
def bintrayApiKey = hasProperty('bintray_apikey') ? bintray_apikey : ''

android
{
    compileSdkVersion 25
    buildToolsVersion '26.0.0'

    dexOptions
    {
        javaMaxHeapSize '2g'
    }

    defaultConfig
    {
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 1
        versionName releaseVersion
    }

    buildTypes
    {
        debug
        {
            debuggable true
        }

        release
        {
            minifyEnabled false
        }
    }

    lintOptions
    {
        abortOnError false
    }

    compileOptions
    {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

group = 'com.mauriciotogneri'
version = releaseVersion

bintray
{
    user = bintrayUser
    key = bintrayApiKey
    configurations = ['archives']
    pkg
    {
        repo = 'maven'
        name = 'greencoffee'
        licenses = ['MIT']
        publish = true
        version
        {
            name = releaseVersion
            vcsTag = releaseVersion
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

dependencies
{
    compile 'io.cucumber:gherkin-jvm-deps:1.0.4'
    compile 'com.android.support.test.espresso:espresso-core:2.2.2'
}